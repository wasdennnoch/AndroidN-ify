import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

ext.buildName = System.getenv("BUILD_TAG") ?: "0.3.0"
ext.buildTime = buildTime()
ext.gitCommit = System.getenv("GIT_COMMIT") ?: "dev"

ext.officialBuild = System.getenv("WDN_PC") ?: "false"
ext.automatedBuild = System.getenv("AUTOMATED_BUILD") ?: "false"
ext.enableUpdater = System.getenv("ENABLE_UPDATER") ?: "false"
ext.updaterUrl = System.getenv("UPDATER_URL") ?: "null"
ext.buildNumber = System.getenv("BUILD_NUMBER") ?: "0"
ext.enableDirectReply = System.getenv("ENABLE_DIRECT_REPLY") ?: "false"

ext.supportLibraryVersion = "24.2.0";

android {
    signingConfigs {
        main
    }
    compileSdkVersion 24
    buildToolsVersion '23.0.3'
    defaultConfig {
        applicationId "tk.wasdennnoch.androidn_ify"
        minSdkVersion 21
        //noinspection OldTargetApi
        targetSdkVersion 23
        versionCode 6
        versionName "$buildName"

        buildConfigField 'String', 'BUILD_TIME', "\"$buildTime\""
        buildConfigField 'String', 'GIT_COMMIT', "\"$gitCommit\""

        buildConfigField 'boolean', 'OFFICIAL_BUILD', "$officialBuild"
        buildConfigField 'boolean', 'AUTOMATED_BUILD', "$automatedBuild"
        buildConfigField 'boolean', 'ENABLE_UPDATER', "$enableUpdater"
        buildConfigField 'String', 'UPDATER_URL', "\"$updaterUrl\""
        buildConfigField 'int', 'BUILD_NUMBER', "$buildNumber"
        buildConfigField 'boolean', 'ENABLE_DIRECT_REPLY', "$enableDirectReply"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.main
        }
        debug {
            versionNameSuffix = "-debug"
            signingConfig signingConfigs.main
        }
    }
    lintOptions {
        abortOnError false
    }
    useLibrary 'org.apache.http.legacy'
}

def propFile = project.file("../../../key.properties")
if (propFile.exists()) {
    def props = new Properties()
    props.load(new FileInputStream(propFile))
    android.signingConfigs.main.storeFile project.file("../../../" + props.keyStore)
    android.signingConfigs.main.storePassword props.keyStorePassword
    android.signingConfigs.main.keyAlias props.keyAlias
    android.signingConfigs.main.keyPassword props.keyPassword
}

def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

dependencies {
    provided 'de.robv.android.xposed:api:81'
    provided 'de.robv.android.xposed:api:81:sources'
    provided files('libs-provide/android_hidden_api_23.jar')
    compile "com.android.support:support-v13:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:palette-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
}