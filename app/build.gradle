import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

ext.buildName = System.getenv("BUILD_NAME") ?: "0.3.0"
ext.buildTime = buildTime()
ext.gitSha = System.getenv("GIT_SHA") ?: "dev"

ext.automatedBuild = System.getenv("AUTOMATED_BUILD") ?: "false"
ext.enableUpdater = System.getenv("ENABLE_UPDATER") ?: "false"
ext.updaterUrl = System.getenv("UPDATER_URL") ?: "null"
ext.buildVersion = System.getenv("BUILD_VERSION") ?: "0"

ext.enableDirectReply = System.getenv("ENABLE_DIRECT_REPLY") ?: "false"

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'
    defaultConfig {
        applicationId "tk.wasdennnoch.androidn_ify"
        minSdkVersion 21
        //noinspection OldTargetApi
        targetSdkVersion 23
        versionCode 6
        versionName "$buildName"

        buildConfigField 'String', 'BUILD_TIME', "\"$buildTime\""
        buildConfigField 'String', 'GIT_SHA', "\"$gitSha\""

        buildConfigField 'boolean', 'AUTOMATED_BUILD', "$automatedBuild"
        buildConfigField 'boolean', 'ENABLE_UPDATER', "$enableUpdater"
        buildConfigField 'String', 'UPDATER_URL', "\"$updaterUrl\""
        buildConfigField 'int', 'BUILD_VERSION', "$buildVersion"
        buildConfigField 'boolean', 'ENABLE_DIRECT_REPLY', "$enableDirectReply"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
    lintOptions {
        abortOnError false
    }
    useLibrary 'org.apache.http.legacy'
}

def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

dependencies {
    provided 'de.robv.android.xposed:api:81'
    provided 'de.robv.android.xposed:api:81:sources'
    provided files('libs-provide/android_hidden_api_23.jar')
    //noinspection GradleDependency
    compile 'com.android.support:support-v13:23.4.0'
    //noinspection GradleDependency
    compile 'com.android.support:recyclerview-v7:23.4.0'
    //noinspection GradleDependency
    compile 'com.android.support:palette-v7:23.4.0'
    //noinspection GradleDependency
    compile 'com.android.support:design:23.4.0'
}
